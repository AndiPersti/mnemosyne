apply plugin: 'com.android.application'

android {
    compileSdkVersion 28

    def versionMajor = 2
    def versionMinor = 2
    def versionPatch = 0

    defaultConfig {
        applicationId "org.mnemosyne"
        versionCode versionMajor * 10000 + versionMinor * 100 + versionPatch
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        minSdkVersion 21
        targetSdkVersion 28
        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_7
            targetCompatibility JavaVersion.VERSION_1_7
        }
        externalNativeBuild {
            cmake {
                arguments '-DANDROID_STL=c++_static'
            }
        }

        // We don't use splits because we want to use different assets per abi.
        flavorDimensions 'abi'
        productFlavors {
            armeabi_v7a {
                dimension 'abi'
                ndk.abiFilter 'armeabi-v7a'
                versionCode = 1
            }
            arm64_v8a {
                dimension 'abi'
                ndk.abiFilter 'arm64-v8a'
                versionCode = 2
            }
            x86 {
                dimension 'abi'
                ndk.abiFilter 'x86'
                versionCode = 3
            }
            x86_64 {
                dimension 'abi'
                ndk.abiFilter 'x86_64'
                versionCode = 4
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources false // Otherwise it will strip our dynamic Python routines.
            proguardFiles getDefaultProguardFile(
                    'proguard-android-optimize.txt'),
                    'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            // Let gradle pack the shared library into the apk.
            jniLibs.srcDirs = ['../dependencies/python/lib']
        }
    }

    externalNativeBuild {
        cmake {
            version '3.10.2'
            path 'src/main/cpp/CMakeLists.txt'
        }
    }

    dependencies {
        implementation 'androidx.appcompat:appcompat:1.1.0'
    }
}

// Make sure each abi has a different version code
android.applicationVariants.all { variant ->
    variant.outputs.each { output ->
        def abiVersion = variant.productFlavors.get(0).versionCode
        output.versionCodeOverride = abiVersion * 1000000 + android.defaultConfig.versionCode
    }
}
